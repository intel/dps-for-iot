import os
Import(['env'])

# Pull down intel-ipsec-mb from github and build the library

# optimized sha512 requires nasm and x86_64
if env['TARGET_ARCH'] != 'x86' and env.Detect('nasm') != None:
    mbenv = env.Clone(tools=['nasm'])

    git = mbenv.Command('./intel-ipsec-mb/.git', None,
                        ['git clone -q -n https://github.com/intel/intel-ipsec-mb.git ext/intel-ipsec-mb'])

    dep = mbenv.Command('./intel-ipsec-mb/README', git,
                        ['git fetch -q', 'git checkout -q master'],
                        chdir = 'ext/intel-ipsec-mb')

    sources = [
        './intel-ipsec-mb/lib/sse/sha512_one_block_sse.asm',
        './intel-ipsec-mb/lib/avx/sha512_one_block_avx.asm'
    ]

    if mbenv['PLATFORM'] == 'win32':
        mbenv['ASFLAGS'] = [
            '-fwin64',
            '-Xvc',
            '-DWIN_ABI',
            '-I./ext/intel-ipsec-mb/lib/'
        ]
    else:
        mbenv['ASFLAGS'] = [
            '-felf64',
            '-Xgnu',
            '-gdwarf',
            '-DLINUX',
            '-D__linux__',
            '-I./ext/intel-ipsec-mb/lib/'
        ]

    objs = mbenv.Object(sources)
    shobjs = mbenv.SharedObject(sources)

    Depends(sources, dep)

else:
    objs = []
    shobjs = []

Return('objs shobjs')
