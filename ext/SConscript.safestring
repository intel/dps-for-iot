import os
Import(['env'])

# Pull down safestringlib from github and build the library

scenv = env.Clone()

scgit = scenv.Command('./safestring/.git', None,
                      ['git clone -n https://github.com/intel/safestringlib.git ext/safestring'])
screl = scenv.Command('./safestring/makefile', scgit,
                      ['git fetch', 'git checkout -q 77b772849eda2321fb0dca56a321e3939930d7b9',
                       'git am ../0001-DPS.patch'],
                      chdir = 'ext/safestring')

sources = [
    './safestring/safeclib/ignore_handler_s.c',
    './safestring/safeclib/safe_mem_constraint.c',
    './safestring/safeclib/safe_str_constraint.c',
    './safestring/safeclib/memset_s.c',
    './safestring/safeclib/memzero_s.c',
    './safestring/safeclib/strcat_s.c',
    './safestring/safeclib/strcmp_s.c',
    './safestring/safeclib/strnlen_s.c',
    './safestring/safeclib/mem_primitives_lib.c'
]

if scenv['PLATFORM'] == 'posix' or scenv['CC'] == 'cl':
    sources += [
        './safestring/safeclib/memcpy_s.c',
        './safestring/safeclib/memmove_s.c',
        './safestring/safeclib/strncpy_s.c'
    ]

scenv['CPPPATH'] = './safestring/include'

objs = scenv.Object(sources)
shobjs = scenv.SharedObject(sources)

Depends(sources, screl)

Return('objs shobjs')
